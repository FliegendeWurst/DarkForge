/**
 * Plugins
 */
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "co.riiid.gradle"

/**
 * Other Scripts
 */

apply from: "gradle/scripts/dependencies.gradle"
apply from: "gradle/scripts/integration.gradle"
apply from: "gradle/scripts/artifacts.gradle"
apply from: "gradle/scripts/optional.gradle"

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url = "https://plugins.gradle.org/m2"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.0.8"
        classpath 'co.riiid:gradle-github-plugin:0.4.2'
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

version = minecraft_version + "-b" + cibuild + "-univeral"
archivesBaseName = cibasename

def githubOwner
def githubRepo = ''
def githubToken = ''
def githubTagName = ''
def githubTargetCommitish = ''
def githubName = ''
def githubPreRelease = true


if (hasProperty("teamcity")) {
    version = minecraft_version + "-" + teamcity["build.number"] + "-univeral"

    githubOwner = teamcity["githubOwner"]
    githubRepo = teamcity["githubProject"]
    githubToken = teamcity["githubToken"]
    githubTagName = teamcity["build.number"]
    githubTargetCommitish = teamcity["branch"]
    githubName = teamcity["build.number"]

    githubPreRelease = teamcity["githubPreRelease"]
}

minecraft {
    version = minecraft_version + "-" + forge_version

    replace "@VERSION@", project.version
    replace "@MCVERSION", minecraft_version

    replaceIn "ModInfo.java"

    runDir = "run"
    mappings = project.mcpVersion

    useDepAts = true
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

github {
    owner = githubOwner
    repo = githubRepo
    token = githubToken
    tagName = githubTagName
    targetCommitish = githubTargetCommitish
    name = githubName
    body = "test"
    prerelease = githubPreRelease
    assets = [
            jar.archivePath
    ]
}

